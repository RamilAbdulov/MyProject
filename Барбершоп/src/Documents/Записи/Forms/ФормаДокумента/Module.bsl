
&НаКлиенте
Процедура УслугаПриИзменении(Элемент)
	Если НЕ Объект.Услуга.Пустая() Тогда
		ИнформацияОбУслуге 	= ПолучитьИнформациюОбУслуге(Объект.Услуга, Объект.Дата);	
		Объект.Длительность = ИнформацияОбУслуге.Длительность;	
		Объект.Сумма 		= ИнформацияОбУслуге.Цена;
	Иначе
		Объект.Длительность = 0;	
		Объект.Сумма 		= 0;

	КонецЕсли;
КонецПроцедуры      

&НаСервереБезКонтекста
Функция ПолучитьИнформациюОбУслуге(Услуга, Дата)                    
	
	Результат = Новый Структура("Длительность,Цена",0,0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ЦеныСрезПоследних.Цена, 0) КАК Цена,
		|	спрУслуги.Длительность КАК Длительность
		|ИЗ
		|	Справочник.Услуги КАК спрУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Цены.СрезПоследних(&Дата, Услуги = &Услуга) КАК ЦеныСрезПоследних
		|		ПО спрУслуги.Ссылка = ЦеныСрезПоследних.Услуги
		|ГДЕ
		|	спрУслуги.Ссылка = &Услуга";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Услуга", Услуга);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(Результат,Выборка);

	КонецЕсли;	                                    
	
	Возврат Результат;
		
КонецФункции // ()


&НаКлиенте
Процедура ВремяНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	
КонецПроцедуры


&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("Изменение_Запись");

КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Цвет  = Новый ХранилищеЗначения(Цвет);
	
КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Цвет = ТекущийОбъект.Цвет.Получить();
	
	
КонецПроцедуры


&НаКлиенте
Процедура ОформитьПродажу(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Сначала сохраните запись";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров=Новый Структура("Основание",Объект.Ссылка);
	ОткрытьФорму("Документ.Продажи.ФормаОбъекта",СтруктураПараметров,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	
КонецПроцедуры

